.segment BANK(BK_MAIN_CODE)
NMI_GAME_PAL:
	BBC	GAME_FLAG, F_SHOW_MAP, :+
	JMP	NMI_GAME_PAL_SHOW_MAP
:

NMI_GAME_PAL_UPDATE_GAME_FIELD_UI:
	PPU_DISABLE
		BIT	PPUSTATUS

		; see https://wiki.nesdev.org/w/index.php/PPU_OAM#Dynamic_RAM_decay
		; "The 2C07 additionally refreshes OAM during the visible portion of the screen 
		;  even if rendering is disabled. Because of this, OAM DMA must be done near the 
		;  beginning of vertical blank on the 2C07, as everywhere else it will conflict 
		;  with this refresh. In exchange, OAM decay does not occur at all on the PAL NES."
		PPU_START_OAM_DMA

		; LDA	#TS_GAME_FIELD+0
		LDA	ANIMATION_INX+0
		MMC3_BANK_SELECT_A  MMC3_CHR_0
		; LDA	#TS_GAME_FIELD+2
		LDA	ANIMATION_INX+1
		MMC3_BANK_SELECT_A  MMC3_CHR_1

		PPU_LOAD_ADDR  $3f00
		LDA 	GAME_BG_PALETTE
		STA 	PPUDATA
		DELAY	11

		LDA	#0
		STA	PPUADDR
		STA	PPUADDR

		MMC3_BANK_SELECT  MMC3_PRG_0, #BK_PPU_TRANS

		BBC	GAME_FLAG, F_TRANSFER_FIELD, :+
		JSR	NMI_TRANSFER_GAME_FIELD_0
		JSR	NMI_TRANSFER_GAME_FIELD_1
	:	JSR	NMI_TRANSFER_GAME_UI


		MMC3_IRQ_DISABLE
		MMC3_IRQ_LATCH 23
		MMC3_IRQ_RELOAD
		MMC3_IRQ_ENABLE

	PPU_DISABLE_BG

	MMC3_BANK_SELECT  MMC3_PRG_0, SAVED_PRG_0

	CLRB	GAME_FLAG, F_TRANSFER_FIELD

	LDA	#0
	STA	IRQ_COUNTER	; reset irq counter
	JMP	RUNIRQ_PAL


NMI_GAME_PAL_SHOW_MAP:
	PPU_DISABLE
		BIT	PPUSTATUS

		; see above
		PPU_START_OAM_DMA
		
		MMC3_BANK_SELECT    MMC3_CHR_0, #TS_GAME_MAP
		LDA	SELECTED_MAP
		ASL
		ORA	#$60
		MMC3_BANK_SELECT_A  MMC3_CHR_1
		
		
		PPU_LOAD_ADDR  $3f00
		LDA 	GAME_BG_PALETTE
		STA 	PPUDATA
		DELAY	11

		LDA	#0
		STA	PPUADDR
		STA	PPUADDR
		
		MMC3_BANK_SELECT  MMC3_PRG_0, #BK_PPU_TRANS
		JSR	NMI_TRANSFER_GAME_UI

		;display elapsed time
		PPU_LOAD_ADDR_XY  1, 16, 17
		LDA	HOURS
		JSR	PPU_DECWRITE2

		PPU_LOAD_ADDR_XY  1, 19, 17
		LDA	MINUTES
		JSR	PPU_DECWRITE2

		PPU_LOAD_ADDR_XY  1, 22, 17
		LDA	SECONDS
		JSR	PPU_DECWRITE2

		; PPU_LOAD_ADDR_XY  1, 15, 17
		; LDA	#$01
		; STA	PPUDATA

		; PPU_LOAD_ADDR_XY  1, 18, 17
		; LDA	#$3A
		; STA	PPUDATA

		; PPU_LOAD_ADDR_XY  1, 21, 17
		; LDA	#$3A
		STA	PPUDATA

		MMC3_IRQ_DISABLE
		MMC3_IRQ_LATCH 180
		MMC3_IRQ_RELOAD
		MMC3_IRQ_ENABLE
		
		PPU_SCROLL_XY  1, 0, 0
	PPU_ENABLE
	
	MMC3_BANK_SELECT  MMC3_PRG_0, SAVED_PRG_0
	
	LDA	#1
	STA	IRQ_COUNTER	; reset irq counter
	JMP	RUNIRQ_PAL
