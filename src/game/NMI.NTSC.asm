.segment BANK(BK_MAIN_CODE)
NMI_GAME_NTSC:
	BBC	GAME_FLAG, F_SHOW_MAP, :+
	JMP	NMI_GAME_NTSC_SHOW_MAP
:

NMI_GAME_NTSC_UPDATE_GAME_FIELD:
	PPU_DISABLE
		BIT	PPUSTATUS

		; LDA	#TS_GAME_FIELD+0
		LDA	ANIMATION_INX+0
		MMC3_BANK_SELECT_A  MMC3_CHR_0
		; LDA	#TS_GAME_FIELD+2
		LDA	ANIMATION_INX+1
		MMC3_BANK_SELECT_A  MMC3_CHR_1

		PPU_LOAD_ADDR  $3f00
		LDA 	GAME_BG_PALETTE
		STA 	PPUDATA
		DELAY	11

		LDA	#0
		STA	PPUADDR
		STA	PPUADDR
		
		MMC3_BANK_SELECT  MMC3_PRG_0, #BK_PPU_TRANS

		BBC	GAME_FLAG, F_TRANSFER_FIELD, :+
		JSR	NMI_TRANSFER_GAME_FIELD_1
		JMP	:++
	:	JSR	NMI_TRANSFER_GAME_FIELD_1_NULL
		NOP
	:
		JSR	NMI_TRANSFER_GAME_UI
		
		DELAY	396 ; Prev Transfer Tile Attribute 
		DELAY	10

		DELAY 16

		PPU_START_OAM_DMA

		MMC3_IRQ_DISABLE
		MMC3_IRQ_LATCH 156
		MMC3_IRQ_RELOAD
		MMC3_IRQ_ENABLE

		DELAY	51
		PPU_SCROLL_XY_FINE_X  0, SCROLL_X, 8
		DELAY	31
	PPU_ENABLE

	MMC3_BANK_SELECT  MMC3_PRG_0, SAVED_PRG_0

	CLRB	GAME_FLAG, F_TRANSFER_FIELD

	LDA	#0
	STA	IRQ_COUNTER	; reset irq counter
	JMP	RUNIRQ_NTSC


NMI_GAME_NTSC_SHOW_MAP:
	PPU_DISABLE
		BIT	PPUSTATUS
		
		MMC3_BANK_SELECT    MMC3_CHR_0, #TS_GAME_MAP
		LDA	SELECTED_MAP
		ASL
		ORA	#$60
		MMC3_BANK_SELECT_A  MMC3_CHR_1

		PPU_LOAD_ADDR  $3f00
		LDA 	GAME_BG_PALETTE
		STA 	PPUDATA
		DELAY	11

		LDA	#0
		STA	PPUADDR
		STA	PPUADDR

		MMC3_BANK_SELECT  MMC3_PRG_0, #BK_PPU_TRANS
		JSR	NMI_TRANSFER_GAME_UI
		
		;display elapsed time
		PPU_LOAD_ADDR_XY  1, 16, 17
		LDA	HOURS
		JSR	PPU_DECWRITE2

		PPU_LOAD_ADDR_XY  1, 19, 17
		LDA	MINUTES
		JSR	PPU_DECWRITE2

		PPU_LOAD_ADDR_XY  1, 22, 17
		LDA	SECONDS
		JSR	PPU_DECWRITE2
		
		PPU_START_OAM_DMA

		MMC3_IRQ_DISABLE
		MMC3_IRQ_LATCH 180
		MMC3_IRQ_RELOAD
		MMC3_IRQ_ENABLE
		
		PPU_SCROLL_XY  1, 0, 0
	PPU_ENABLE
	
	MMC3_BANK_SELECT  MMC3_PRG_0, SAVED_PRG_0
	
	LDA	#0
	STA	IRQ_COUNTER	; reset irq counter
	JMP	RUNIRQ_NTSC
